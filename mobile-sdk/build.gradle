/*---------------------------------------------------------------------------------------------
* Copyright (c) Bentley Systems, Incorporated. All rights reserved.
* See LICENSE.md in the project root for license terms and full copyright notice.
*--------------------------------------------------------------------------------------------*/
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'io.gitlab.arturbosch.detekt' version '1.20.0-RC2'
    id 'org.jetbrains.dokka' version '1.6.21'
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 24
        targetSdk 31
        versionCode 1
        versionName '0.10.23'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation "androidx.fragment:fragment-ktx:1.4.1"
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'com.google.android.gms:play-services-tasks:18.0.1'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.eclipsesource.minimal-json:minimal-json:0.9.5'
    implementation 'com.google.android.gms:play-services-base:18.0.1'
    implementation 'com.google.android.gms:play-services-location:19.0.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.5.0"
    api 'com.github.itwin:mobile-native-android:3.2.8'

    // testImplementation 'junit:junit:4.+'
    // androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    // androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/itwin/mobile-sdk-android")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
                }
            }
        }
        publications {
            // Creates a Maven publication called 'release'.
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.itwin'
                artifactId = 'mobile-sdk-android'
                version = '0.10.23'
            }
            // Creates a Maven publication called 'debug'.
            debug(MavenPublication) {
                from components.debug
                artifact(sourceJar)
                groupId = 'com.github.itwin'
                artifactId = 'mobile-sdk-android'
                version = '0.10.23-debug'
            }
        }
    }
}

detekt {
    toolVersion = "1.20.0-RC2"
    config = files("detekt.yml")
    buildUponDefaultConfig = true
}