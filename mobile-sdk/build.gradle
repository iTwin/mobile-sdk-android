/*---------------------------------------------------------------------------------------------
* Copyright (c) Bentley Systems, Incorporated. All rights reserved.
* See LICENSE.md in the project root for license terms and full copyright notice.
*--------------------------------------------------------------------------------------------*/
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'io.gitlab.arturbosch.detekt' version '1.22.0'
    id 'org.jetbrains.dokka' version '1.6.21'
}

android {
    compileSdk 33

    defaultConfig {
        minSdk 28
        targetSdk 33
        versionCode 1
        versionName '0.22.17'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles 'consumer-rules.pro'

        // NOTE: this is here to suppress an error when running detekt, consumers of the mobile-sdk are expected to have an app-specific entry in their build.gradle.
        manifestPlaceholders = ['appAuthRedirectScheme': "com.github.itwin.mobile-sdk-android"]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    lintOptions {
        // Note: suppressing this lint option as we seem to be getting false positives in our code when running `detekt build`, and adding @SuppressLint in the code didn't work either.
        disable 'JavascriptInterface'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation "androidx.fragment:fragment-ktx:1.5.4"
    implementation 'androidx.webkit:webkit:1.5.0'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'com.google.android.gms:play-services-tasks:18.0.2'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.google.android.gms:play-services-base:18.1.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.1'
    implementation 'net.openid:appauth:0.11.1'
    api 'com.github.itwin:mobile-native-android:4.10.27'

    // testImplementation 'junit:junit:4.+'
    // androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    // androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/itwin/mobile-sdk-android")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
                }
            }
        }
        publications {
            // Creates a Maven publication called 'release'.
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.itwin.mobilesdk'
                artifactId = 'mobile-sdk-android'
                version = '0.22.17'
            }
            // Creates a Maven publication called 'debug'.
            debug(MavenPublication) {
                from components.debug
                artifact(sourceJar)
                groupId = 'com.github.itwin.mobilesdk'
                artifactId = 'mobile-sdk-android'
                version = '0.22.17-debug'
            }
        }
    }
}

detekt {
    toolVersion = "1.22.0"
    config = files("detekt.yml")
    buildUponDefaultConfig = true
}